/*
 * AppCompatWebComponents (https://github.com/androidovshchik/AppCompatWebComponents)
 * Copyright (c) 2019. Vlad Kalyuzhnyu <vladkalyuzhnyu@gmail.com>
 */

apply from: "$rootDir/module.gradle"
apply from: "$rootDir/publish.gradle"

android {
    flavorDimensions 'source'
    productFlavors {
        core {
            dimension 'source'
            minSdkVersion 16
            buildConfigField("boolean", "SHARED", "false")
            buildConfigField("boolean", "LITE", "false")
        }
        shared {
            dimension 'source'
            minSdkVersion 16
            buildConfigField("boolean", "SHARED", "true")
            buildConfigField("boolean", "LITE", "false")
        }
        lite {
            dimension 'source'
            minSdkVersion 14
            buildConfigField("boolean", "SHARED", "false")
            buildConfigField("boolean", "LITE", "true")
        }
    }
}

dependencies {
    implementation project(':webcompat')
    // contains only armeabi-v7a and x86 binaries
    coreApi ("org.xwalk:xwalk_core_library:$crosswalk_version") {
        exclude group: 'com.android.support'
    }
    // doesn't contain binaries
    sharedApi ("org.xwalk:xwalk_shared_library:$crosswalk_version") {
        exclude group: 'com.android.support'
    }
    // doesn't contain 64-bit binaries
    liteApi ("org.xwalk:xwalk_core_library_canary:$crosswalk_lite_version") {
        exclude group: 'com.android.support'
    }
}

afterEvaluate {
    generateCoreDebugResValues.enabled = false
    generateCoreReleaseResValues.enabled = false
    generateSharedDebugResValues.enabled = false
    generateSharedReleaseResValues.enabled = false
    generateLiteDebugResValues.enabled = false
    generateLiteReleaseResValues.enabled = false
}