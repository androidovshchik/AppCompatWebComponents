/*
 * AppCompatWebComponents (https://github.com/androidovshchik/AppCompatWebComponents)
 * Copyright (c) 2019. Vlad Kalyuzhnyu <vladkalyuzhnyu@gmail.com>
 */

apply from: "$rootDir/module.gradle"
apply from: "$rootDir/publish.gradle"

android {
    flavorDimensions 'source'
    productFlavors {
        stable {
            dimension 'source'
            // 32-bit architectures require 16 while 64-bit require 21
            minSdkVersion 16
            buildConfigField("boolean", "BETA", "false")
            buildConfigField("boolean", "NIGHTLY", "false")
        }
        beta {
            dimension 'source'
            minSdkVersion 16
            buildConfigField("boolean", "BETA", "true")
            buildConfigField("boolean", "NIGHTLY", "false")
        }
        nightly {
            dimension 'source'
            minSdkVersion 16
            buildConfigField("boolean", "BETA", "false")
            buildConfigField("boolean", "NIGHTLY", "true")
        }
    }
}

dependencies {
    implementation project(':appcompat')
    stableApi ("org.mozilla.geckoview:geckoview-armeabi-v7a:$gecko_version") {
        exclude group: 'com.android.support'
    }
    // contains armeabi-v7a, arm64-v8a, x86, x86_64 binaries
    betaApi ("org.mozilla.geckoview:geckoview-beta:$gecko_beta_version") {
        exclude group: 'com.android.support'
    }
    // contains armeabi-v7a, arm64-v8a, x86, x86_64 binaries
    nightlyApi ("org.mozilla.geckoview:geckoview-nightly:$gecko_nightly_version") {
        exclude group: 'com.android.support'
    }
}

afterEvaluate {
    generateStableDebugResValues.enabled = false
    generateStableReleaseResValues.enabled = false
    generateBetaDebugResValues.enabled = false
    generateBetaReleaseResValues.enabled = false
    generateNightlyDebugResValues.enabled = false
    generateNightlyReleaseResValues.enabled = false
}